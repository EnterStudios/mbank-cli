#!/usr/bin/perl

# Copyright © 2006-2014 Jakub Wilk <jwilk@jwilk.net>
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the “Software”), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED “AS IS”, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

use strict;
use warnings;
no encoding;

use Encode qw();
use Encode::Locale qw();
use English qw(-no_match_vars);
use File::Basename qw(dirname);
use Getopt::Long qw(:config gnu_compat permute no_getopt_compat no_ignore_case);
use HTML::TreeBuilder ();
use HTTP::Cookies ();
use HTTP::Request::Common qw(GET POST);
use IO::Socket::SSL ();
use JSON qw(encode_json decode_json);
use IPC::Run;
use LWP::UserAgent;
use Net::HTTPS ();  # must be loaded after IO::Socket::SSL
use Text::ParseWords ();

# ==========================
# logging and error handling
# ==========================

my $opt_verbose = 0;
my $opt_debug_dir = undef;

sub write_log
{
    my ($message) = @_;
    defined($opt_debug_dir) or return;
    my $path = "$opt_debug_dir/log";
    open(my $log, '>>', $path)
        or die "$path: $ERRNO";
    print { $log } "$message\n";
    close($log)
        or die "$path: $ERRNO";
    return;
}

sub debug
{
    my ($message) = @_;
    $message = "* $message";
    write_log($message);
    if ($opt_verbose) {
        print {*STDERR} "$message\n";
    }
    return;
}

sub user_error
{
    my ($message) = @_;
    if (defined($message)) {
        write_log($message);
        print {*STDERR} "$message\n";
    }
    exit(1);
}

sub config_error
{
    my ($config, $message) = @_;
    my $config_path;
    if (ref $config) {
        $config_path = $config->{__path__};
    } else {
        $config_path = $config;
    }
    $message = "$config_path: $message";
    return user_error($message);
}

sub http_error
{
    my ($request, $response) = @_;
    my $message = sprintf(
        'HTTP error %d while processing request <%s %s>',
        $response->code,
        $request->method,
        $request->uri
    );
    if ($EVAL_ERROR) {
        my $extra = $EVAL_ERROR;
        $extra =~ s/\n+$//;
        $extra =~ s/\n+/\n/g;
        if ($extra !~ /[(]/ and $IO::Socket::SSL::SSL_ERROR) {
            # https://bugs.debian.org/746686
            $extra .= "\n$IO::Socket::SSL::SSL_ERROR";
        }
        $extra =~ s/\n+$//;
        $extra =~ s/^/| /gm;
        $message .= "\n$extra\n";
    }
    write_log($message);
    Carp::cluck($message);
    exit(2);
}

sub scraping_error
{
    my ($message) = @_;
    $message = "Scraping error: $message";
    write_log($message);
    Carp::cluck($message);
    exit(3);
}

# =========
# encodings
# =========

my %_encoding_fallback = (
    0x104 => 'A', 0x105 => 'a', # letter A with ogonek
    0x0c1 => 'A', 0x0e1 => 'a', # letter A with acute
    0x0c4 => 'A', 0x0e4 => 'a', # letter A with diaeresis
    0x106 => 'C', 0x107 => 'c', # letter C with acute
    0x10c => 'C', 0x10d => 'c', # letter C with caron
    0x10e => 'D', 0x10f => 'd', # letter D with caron
    0x118 => 'E', 0x119 => 'e', # letter E with ogonek
    0x0c9 => 'E', 0x0e9 => 'e', # letter E with acute
    0x11a => 'E', 0x11b => 'e', # letter E with caron
    0x0cd => 'I', 0x0ed => 'i', # letter I with acute
    0x141 => 'L', 0x142 => 'l', # letter L with stroke
    0x139 => 'L', 0x13a => 'l', # letter L with acute
    0x13d => 'L', 0x13e => 'l', # letter L with caron
    0x143 => 'N', 0x144 => 'n', # letter N with acute
    0x147 => 'N', 0x148 => 'n', # letter N with caron
    0x0d3 => 'O', 0x0f3 => 'o', # letter O with acute
    0x0d4 => 'O', 0x0f4 => 'o', # letter O with circumflex
    0x154 => 'R', 0x155 => 'r', # letter R with acute
    0x158 => 'R', 0x159 => 'r', # letter R with caron
    0x15a => 'S', 0x15b => 's', # letter S with acute
    0x160 => 'S', 0x161 => 's', # letter S with caron
    0x164 => 'T', 0x165 => 't', # letter T with caron
    0x0da => 'U', 0x0fa => 'u', # letter U with acute
    0x16e => 'U', 0x16f => 'u', # letter U with ring above
    0x0dd => 'Y', 0x0fd => 'y', # letter Y with acute
    0x179 => 'Z', 0x17a => 'z', # letter Z with acute
    0x17b => 'Z', 0x17c => 'z', # letter Z with dot above
    0x17d => 'Z', 0x17e => 'z', # letter Z with caron
);

sub _encoding_fallback
{
    my ($u) = @_;
    return
      $_encoding_fallback{$u}
      // sprintf('<U+%04X>', $u);
}

sub bytes_to_unicode
{
    my ($u, $encoding) = @_;
    $encoding //= 'locale';
    return Encode::decode($encoding, $u);
}

sub unicode_to_bytes
{
    my ($s, $encoding) = @_;
    $encoding //= 'locale';
    return Encode::encode($encoding, $s, \&_encoding_fallback);
}

# ====
# HTTP
# ====

my $ua = undef;

my $http_product_name = 'Mozilla/5.0 (Windows NT 6.1; rv:17.0) Gecko/20100101 Firefox/17.0';
my $http_read_size_hint = 1 << 20;  # 1 MiB
my $http_timeout = 30;

sub http_init
{
    my %options = @_;
    my $host = delete $options{host} or die 'http_init(): missing host';
    my $cookie_jar_path = delete $options{cookie_jar} // '/dev/null';
    my $ca_path = delete $options{ca} or die 'http_init(): missing ca';
    die "http_init(): unexpected options: @{[keys(%options)]}" if %options;
    if ($Net::HTTPS::SSL_SOCKET_CLASS ne 'IO::Socket::SSL') {
        # should not happen, but better safe than sorry
        die "\$Net::HTTPS::SSL_SOCKET_CLASS == $Net::HTTPS::SSL_SOCKET_CLASS"
    };
    for my $key (grep { m/^HTTPS_/ } keys(%ENV)) {
        # https://bugs.debian.org/746579
        delete $ENV{$key};
    }
    my @ssl_options = (
        SSL_cipher_list => 'HIGH:!aNULL:!eNULL',
        SSL_ca_file => $ca_path,
        SSL_ca_path => \undef,
        SSL_verify_mode => 1,
        SSL_verifycn_name => $host,
        SSL_verifycn_scheme => {
            check_cn => 0,
            wildcards_in_alt => 0,
            wildcards_in_cn => 0,
            # FIXME: LWP::protocol::https (>= 6.0) stomps on SSL_verifycn_scheme
            # https://bugs.debian.org/747225
        },
    );
    IO::Socket::SSL::set_ctx_defaults(@ssl_options);
    my $cookie_jar;
    if (defined $cookie_jar_path) {
        $cookie_jar = HTTP::Cookies->new(
            file => $cookie_jar_path,
            autosave => 1,
            ignore_discard => 1
        );
    } else {
        $cookie_jar = {};
    };
    my $ua = LWP::UserAgent->new(
        agent => $http_product_name,
        cookie_jar => $cookie_jar,
        requests_redirectable => ['GET', 'POST'],
        protocols_allowed => ['https'],
        timeout => $http_timeout,
    );
    if ($IO::Socket::SSL::VERSION >= 1.969) {
        IO::Socket::SSL::set_args_filter_hack('use_defaults');
    } elsif ($LWP::UserAgent::VERSION >= 6) {
        $ua->ssl_opts(
            verify_hostname => 1,
            @ssl_options,
        )
    };
    return $ua;
}

sub download
{
    my ($request) = @_;
    my $message = sprintf(
        '%s %s',
        $request->method,
        $request->uri
    );
    debug($message);
    my $response = $ua->request($request, undef, $http_read_size_hint);
    my $content = $response->content;
    $content =~ s/\r//g;
    if (defined $opt_debug_dir) {
        my $is_json = $response->header('Content-Type') =~ m{\Aapplication/json(;|\Z)};
        my $default_ext = $is_json ? 'json' : 'html';
        my $path = $request->uri;
        $path =~ s{^\w+://.*?/}{};
        $path =~ s/[?].*//;
        $path =~ s/[^[:alnum:].]/_/g;
        $path =~ s/(?:[.]\w+)?$/.$default_ext/;
        if ($path eq '.html') {
            $path = 'index.html';
        }
        $path = "$opt_debug_dir/$path";
        open(my $fh, '>', "$path")
            or die "$path: $ERRNO";
        print { $fh } $content;
        close($fh)
            or die "$path: $ERRNO";
    }
    if (not $response->is_success) {
        http_error($request, $response);
    }
    my $url = $response->request->uri;
    return {
        response => $response,
        content => $content,
        url => $url,
    };
}

sub simple_download
{
    my ($request) = @_;
    my $message = sprintf(
        'simple %s %s',
        $request->method,
        $request->uri
    );
    debug($message);
    my $response = $ua->simple_request($request, undef, $http_read_size_hint);
    if ($response->is_error) {
        http_error($request, $response);
    }
    return $response;
}

# ===========================
# configuration file handling
# ===========================

sub read_config
{
    my ($path) = @_;
    open(my $fh, '<', $path)
        or die "$path: $ERRNO";
    my $config = _read_config($fh, $path);
    close($fh)
        or die "$path: $ERRNO";
    return $config;
}

sub _read_config
{
    my ($fh, $path) = @_;
    my $pgp = undef;
    my $config = {
        __pgp__ => [],
        __path__ => $path,
    };
    while (<$fh>) {
        chomp;
        if (defined $pgp) {
            $pgp .= "$_\n";
            if ($_ eq '-----END PGP MESSAGE-----') {
                push(@{$config->{__pgp__}}, $pgp);
                $pgp = undef;
            }
        } elsif ($_ eq '-----BEGIN PGP MESSAGE-----') {
            $pgp = "$_\n";
        } elsif (m/^(?:#|\s*)$/) {
            next;
        } elsif (m/^\s*([\w-]+)\s+(.*\S)\s*$/) {
            my ($key, $value) = ($1, $2);
            $key = lc($key);
            ($value) = Text::ParseWords::parse_line('^$', 0, $value);
            $config->{$key} = $value;
        } else {
            config_error($path, "syntax error: $_");
        }
    }
    return $config;
}

sub _decrypt_config
{
    my ($config) = @_;
    my $pgp_chunks = delete $config->{__pgp__};
    foreach my $encrypted_data (@{$pgp_chunks}) {
        my $decrypted_data;
        IPC::Run::run(
            ['gpg', '-d'],
            '<', \$encrypted_data,
            '>', \$decrypted_data,
        ) or die 'gpg -d failed';
        my @decrypted_data = split(/\n/, $decrypted_data);
        foreach (@decrypted_data) {
            if (m/^(?:#|\s*)$/) {
                next;
            } elsif (m/^\s*([\w-]+)\s+(.*\S)\s*$/) {
                my ($key, $value) = ($1, $2);
                $key = lc($key);
                ($value) = Text::ParseWords::parse_line('^$', 0, $value);
                $config->{$key} = $value;
            } else {
                config_error($config, "syntax error in encrypted part: $_");
            }
        }
    }
    return $config;
}

sub get_config_var
{
    my ($config, $var) = @_;
    if (exists $config->{$var}) {
        return $config->{$var};
    }
    if (@{$config->{__pgp__}}) {
        _decrypt_config($config);
    }
    return $config->{$var};
}

# ============
# misc parsing
# ============

sub parse_amount
{
    my ($s) = @_;
    $s =~ m/\A-?\d+,\d{2}\Z/ or return;
    $s =~ y/,/./;
    return $s;
}

sub wildcards_to_regexp
{
    my (@wildcards) = @_;
    my $re = join('|',
        map { quotemeta($_) } @wildcards
    );
    $re =~ s/\\[*]/.*/g;
    $re = qr{^(?i:($re))$};
    return $re;
}

sub find_html_class
{
    my ($element, $class) = @_;
    my $re = qr/(?:\A|\s)\Q$class\E(?:\s|\Z)/;
    return $element->look_down('class', $re);
}

# =======================
# command implementations
# =======================

my $cc = 'pl_PL';
my ($tld, $lang) = ($cc =~ m/(\w+)_(\w+)/);
$lang = lc $lang;
my $mbank_host = $ENV{MBANK_CLI_HOST} // "online.mbank.$tld";
my $base_url = $ENV{MBANK_CLI_BASE_URL} // "https://$mbank_host/$lang";

my $opt_multi = undef;
my $opt_all = undef;

my @json_headers = (
    'Accept' => 'application/json, text/javascript, */*; q=0.01',
    'Content-Type' => 'application/json; charset=UTF-8',
    'X-Requested-With' => 'XMLHttpRequest',
);

my @ajax_html_headers = (
    'Accept' => 'text/html, */*; q=0.01',
    'Content-Type' => 'application/x-www-form-urlencoded; charset=UTF-8',
    'X-Requested-With' => 'XMLHttpRequest',
);

sub do_login
{
    my ($cfg) = @_;
    my $request = GET("$base_url");
    my $doc = download($request);
    if ($doc->{url} =~ m{/Login$}) {
        if (not defined $cfg) {
            return;
        }
        debug('logging in...');
        my ($seed) = ($doc->{content} =~ m/entrypoint[.]initialize[(]'([\w=-]+)'[)]/)
            or scraping_error('login.seed');
        my $login_url = $doc->{url};
        my $login = get_config_var($cfg, 'login')
            // config_error($cfg, 'missing login');
        my $password = get_config_var($cfg, 'password')
            // config_error($cfg, 'missing password');
        $request = POST(
            "$base_url/LoginMain/Account/JsonLogin",
            @json_headers,
            'Referer' => $login_url,
            'Content' => encode_json({
                UserName => $login,
                Password => $password,
                Seed => $seed,
                Lang => '',
            }),
        );
        $doc = download($request);
        my $login_json = decode_json($doc->{content});
        if ($login_json->{successful}) {
            $request = GET("$base_url");
            $doc = download($request);
        } else {
            my $message = $login_json->{errorMessageTitle} // 'unknown error';
            user_error("Login failed: $message");
        }
    }
    my $tabid = undef;
    $ua->cookie_jar->scan(
        sub {
            my ($version, $key, $value, $path, $domain) = @_;
            if (($domain eq $mbank_host) and ($key eq 'mBank_tabId')) {
                $tabid = $value;
            }
        }
    );
    defined($tabid)
        or scraping_error('login.tabid');
    my ($arvt) = ($doc->{content} =~ m{<meta\s+content="([\w/+]+)"\s+name="__AjaxRequestVerificationToken">})
        or scraping_error('login.arvt');
    debug('logged in');
    return {
        tabid => $tabid,
        arvt => $arvt,
        url => $doc->{url},
    };
}

sub do_list
{
    my ($login_info) = @_;
    my $request = POST(
        "$base_url/MyDesktop/Desktop/GetAccountsList",
        'X-Tab-Id' => $login_info->{tabid},
        'X-Request-Verification-Token' => $login_info->{arvt},
        @json_headers,
        'Referer' => $login_info->{url},
        Content => '{}',
    );
    my $doc = download($request);
    my $json = decode_json($doc->{content});
    my @accounts = @{$json->{accountDetailsList}};
    for my $account (@accounts) {
        my $name = $account->{ProductName} // scraping_error('list.product-name');
        my $subtitle = $account->{SubTitle} // '';
        if ($subtitle ne '') {
            $name .= " - $subtitle";
        }
        my $number = $account->{AccountNumber} // scraping_error('list.account-number');
        my $balance = $account->{Balance};
        my $available = $account->{AvailableBalance};
        $balance = parse_amount($balance) // scraping_error("list.balance: $balance");
        $available = parse_amount($available) // scraping_error("list.available: $available");
        my $currency = $account->{Currency};
        printf("%s\t%32s\t%8.2f %s\t%8.2f %s\n", $name, $number, $balance, $currency, $available, $currency);
    }
    return;
}

sub do_future
{
    my ($login_info, @args) = @_;
    if ($opt_all) {
        @args = '*';
    } elsif (scalar(@args) < 1) {
        user_error('No account selected.');
    }
    my $request = POST(
        "$base_url/FutureOperations/Calendar/OPER_GetFutureTransfers",
        'X-Tab-Id' => $login_info->{tabid},
        'X-Request-Verification-Token' => $login_info->{arvt},
        @json_headers,
        'Referer' => $login_info->{url},
        Content => '{"getFilter":true}',
    );
    my $doc = download($request);
    my $json = decode_json($doc->{content});
    my $selected_re = wildcards_to_regexp(@args);
    my $account_info = $json->{filter}->{filterAccounts}
        // scraping_error('future.filter');
    my %selected_accounts = ();
    foreach my $account (@{$account_info}) {
        my $name = $account->{accountName}
            // scraping_error('future.filter.account-name');
        my $number = $account->{accountNumber}
            // scraping_error('future.filter.account-number');
        if ($name =~ $selected_re) {
            debug("selected account: $number");
            $selected_accounts{$number} = $name;
        } else {
            debug("deselected account: $number");
        }
    }
    if (not %selected_accounts) {
        user_error('No matching accounts.')
    }
    my $use_multi =
        $opt_multi ||
        $opt_all ||
        (scalar(keys %selected_accounts) > 1);
    my $transfer_info = $json->{transferInfos}
        // scraping_error('future.transfer');
    for my $transfer (@{$transfer_info}) {
        my $number = $transfer->{accountNumber}
            // scraping_error('future.transfer.account-number');
        my $name = $selected_accounts{$number};
        defined $name or next;
        if ($use_multi) {
            print("$name\t");
        }
        my $timestamp = $transfer->{date};
        my ($date, $time) = ($timestamp =~ m/\A(\d\d\d\d-\d\d-\d\d)T(\d\d:\d\d:\d\d[.]\d+)\Z/)
            or scraping_error('future.transfer.date');
        if ($time =~ m/\A[0:.]+\Z/) {
            # boring
            $timestamp = $date;
        }
        my $recipient = $transfer->{benef}
            // scraping_error('future.transfer.recipient');
        for my $key (qw(benefAddress benefCity)) {
            my $recipient_loc = $transfer->{$key} // '';
            if (length($recipient_loc) > 0) {
                $recipient .= "; $recipient_loc";
            }
        }
        my $description = $transfer->{description}
            // scraping_error('future.transfter.description');
        $description = unicode_to_bytes($description);
        my $amount = $transfer->{amount}
            // scraping_error('future.transfter.amount');
        my $currency = $transfer->{currency}
            // scraping_error('future.transfter.currency');
        my $fmt_amount = sprintf('%8.2f %s', $amount, $currency);
        my $status = $transfer->{transferType}
            // scraping_error('future.transfter.type');
        print("$timestamp\t$recipient\t$description\t$fmt_amount\t$status\n");
    }
    return;
}

sub do_logout
{
    my ($login_info) = @_;
    debug('logging out...');
    my $request = POST(
        "$base_url/LoginMain/Account/LazyLogout",
        'X-Tab-Id' => $login_info->{tabid},
        'X-Request-Verification-Token' => $login_info->{arvt},
        @json_headers,
        'Referer' => $login_info->{url},
    );
    my $doc = download($request);
    my $json = decode_json($doc->{content});
    $json->{lazy}
        or scraping_error('logout.lazy');
    $request = GET(
        "$base_url/LoginMain/Account/Logout",
        'Referer' => $login_info->{url},
    );
    my $response = simple_download($request);
    $response->is_redirect
        or scraping_error('logout.redirect');
    debug('successful logout');
    $ua->cookie_jar->clear();
    debug('cookies have been wiped out');
    return;
}

sub do_cards
{
    my ($login_info) = @_;
    my $request = POST(
        "$base_url/Cards/Cards/IndexData",
        'X-Tab-Id' => $login_info->{tabid},
        'X-Request-Verification-Token' => $login_info->{arvt},
        @ajax_html_headers,
        'Referer' => $login_info->{url},
        Content => '',
    );
    my $doc = download($request);
    my $e_root = HTML::TreeBuilder->new_from_content($doc->{content});
    my @e_cards = find_html_class($e_root, 'card-properties');
    for my $e_card (@e_cards) {
        my $e_name = find_html_class($e_card, 'card-name')
            // scraping_error('cards.name');
        my $name = $e_name->as_trimmed_text();
        my $e_number = find_html_class($e_card, 'card-number')
            // scraping_error('cards.number');
        my $number = $e_number->as_trimmed_text();
        my $e_owner = find_html_class($e_card, 'card-owner')
            // scraping_error('cards.owner');
        my $owner = $e_owner->as_trimmed_text();
        my $e_amount = find_html_class($e_card, 'card-amount')
            // scraping_error('cards.amount.1');
        my $amount = $e_amount->as_trimmed_text();
        ($amount, my $currency) = ($amount =~ m/\s*(\S+)\s+(\S+)\Z/)
            or scraping_error('cards.amount.2');
        $amount = parse_amount($amount)
            // scraping_error('cards.amount.3');
        printf("%s\t%s\t\%s\t%8.2f %s\n", $name, $number, $owner, $amount, $currency);
    }
    return;
}

# ============
# main program
# ============

my $here = dirname($PROGRAM_NAME);
chdir($here) or die "$here: $ERRNO";

my $opt_config = './mbank-cli.conf';
my $opt_cookie_jar = './cookie-jar.txt';
my $opt_from = undef;
my $opt_to = undef;
my $opt_range = undef;

sub show_help
{
    print { *STDERR } <<'EOF' ;
Usage:
  mbank-cli [list]
  mbank-cli history [--range <start-date> <end-date> | --from <start-date> [--to <end-date>] ] {<account> | -M <account>... | -A}
  mbank-cli future {<account> | -M <account>... | -A}
  mbank-cli blocked {<account> | -M <account>... | -A}
  mbank-cli deposits
  mbank-cli funds
  mbank-cli notices
  mbank-cli rename <account> <new-name>
  mbank-cli logout
  mbank-cli void

Common options:
  --verbose
  --debug <debug-directory>
  --config <config-file>
  --cookie-jar <cookie-jar-file>
EOF
    exit();
}

GetOptions(
    'verbose' => \$opt_verbose,
    'debug=s' => \$opt_debug_dir,
    'config=s' => \$opt_config,
    'cookie-jar=s' => \$opt_cookie_jar,
    'from=s' => sub {
        (undef, my $date) = @_;
        $opt_from = fix_date($date);
        $opt_range = 0;
    },
    'to=s' => sub {
        (undef, my $date) = @_;
        $opt_to = fix_date($date);
        $opt_range = 0;
    },
    'range=s{2}' => sub {
        (undef, my $date) = @_;
        if ($opt_range) {
          $opt_from = fix_date($date);
          $opt_to = undef;
          $opt_range = 1;
        }
        else
        {
          $opt_to = fix_date($date);
          $opt_range = 0;
        }
    },
    'M|multiple-accounts' => \$opt_multi,
    'A|all-accounts' => \$opt_all,
    'h|help' => \&show_help,
) or user_error();

my $cfg = read_config($opt_config);

$ua = http_init(
    host => $mbank_host,
    cookie_jar => $opt_cookie_jar,
    ca => './ca.crt',
);

my ($command, @args) = @ARGV;
$command //= 'list';
debug("selected command: $command");

if ($command eq 'void') {
    # Nothing to do!
} elsif ($command eq 'list') {
    my $login_info = do_login($cfg);
    do_list($login_info);
} elsif ($command eq 'future') {
    my $login_info = do_login($cfg);
    do_future($login_info, @args);
} elsif ($command eq 'cards') {
    my $login_info = do_login($cfg);
    do_cards($login_info, @args);
} elsif ($command eq 'logout') {
    my $login_info = do_login();
    if (not defined $login_info) {
        user_error('The user is not logged in.')
    }
    do_logout($login_info);
} elsif (grep { $command eq $_ } qw(history blocked deposits funds notices rename)) {
    user_error("Command not implemented: $command");
} else {
    user_error("Invalid command: $command");
}

# vim:ts=4 sw=4 et
